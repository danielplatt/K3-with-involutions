/* 
This is the alternative construction as pointed out
by the anonymous referee.

This file finds examples of a quartic K3 surface
defined over the Rationals, with Picard number 2 and a
holomorphic involution. These examples are constructed
by generating a random curve of genus 2 in P1xP1 and 
embedding this via the Segre embedding in P3. There 
we look for a quartic surface containing this curve.
If the quartic surface is smooth it will be a K3 surface
containing a genus 2 curve. The linear system of this 
curve gives a 2-1 cover of the projective plane, 
inducing the holomorphic involution.

The function can take quite a lot of time, because it 
uses the time consuming function Deg2K3RankUpperBound. 
This is needed to check if the surface has Picard rank 2. 
If not, the process starts again so if we are unlucky 
it can take more than one hour...
*/

load Deg2K3RankUpperBound;
// function to calculate Picard number

Q:=RationalField();
P3<x,y,z,w>:=ProjectiveSpace(Q,3);
L:=LinearSystem(P3,2);

P2<s,t,u> := ProjectiveSpace(Q,2);
RR:=CoordinateRing(P2);

K<a,b>:=FieldOfFractions(PolynomialRing(Q,2));
// will be used to find the branched curve..

g:=x*w-y*z;
g1:=x*y-z^2;
g2:=y^2-z*w;
// the equations of the twisted cubic curve

repeat
// as long as the surface we've found doesnt have Pic number 2,
// we keep searching


// First we look for a smooth quartic surface with an 
// irreducible smooth curve of genus 2
  repeat
    repeat
      repeat
        h := Random(L)*g + Random(L)*g1 + Random(L)*g2;
        X := Scheme(P3,h);
      until IsNonsingular(X);              
      Z := Scheme(X,g);
    until #IrreducibleComponents(Z) eq 2;
    C := IrreducibleComponents(Z)[1];
  until IsNonsingular(C);

// Then we define the double cover map pi
  DivC := Divisor(X,C);
  LC := RiemannRochBasis(DivC);
  pi := map < X -> P2 | [LC[1],LC[2],LC[3]] >;
  pieq := DefiningEquations(pi);
  B := BaseScheme(pi); // this will be the part where the 
                     // defining equations of pi are all 0.
  
// Now we need to find the model as a double cover, i.e. we 
// will search for branched curve in P2 of the double cover.
// We will do this by searching for the inverse image of the
// generic point (a:b:1) and solve for which points we only 
// have one point in the inverse image. This is why we use
// the field K:=Q(a,b).
  XK:=BaseChange(X,K);
  P3K<z0,z1,z2,z3>:=AmbientSpace(XK);

// Defining equations of pi:  
  pe1 := Evaluate(pieq[1],[z0,z1,z2,z3]);
  pe2 := Evaluate(pieq[2],[z0,z1,z2,z3]);
  pe3 := Evaluate(pieq[3],[z0,z1,z2,z3]);

// equations which have to hold for the inverse image
  eqF1 := pe1-a*pe3;
  eqF2 := pe2-b*pe3;
// Inverse image of the generic point (a:b:1):
  pXF:= XK meet Scheme(P3K,[eqF1,eqF2]);
  XF:=Difference(pXF,Scheme(P3K,Equations(B)));
// Note that this defines a zero dimensional scheme
// with two points over the field K. 
// In particular, one can check that the equations of pi
// only depend on the first coordinates and hence magma 
// gives us a quadric equation in y3, from which we can 
// deduce where the curve is branched. This is what we do below:

  for p in Equations(XF) do
    if Degree(p) eq 2 then
      cf:=ClearDenominators(p);
    end if;
  end for;
  cf1:=MonomialCoefficient(cf,z2^2); 
  cf2:=MonomialCoefficient(cf,z2*z3);
  cf3:=MonomialCoefficient(cf,z3^2);
  
  if cf2 ne 0 then // if cf2=0 then the involution is induced by
                   // a linear map and so the Picard number definitely
                   // will not be 2. So we skip the below calculations
                   // and search for a new curve and surface.
    D:=cf2^2-4*cf1*cf3; // determinant of abc-formula, so this exactly 
                        // tells where we have only one solution.
    fD:=Factorization(Numerator(D));
// Now we can find the branched curve:
    for p in fD do
      if Degree(p[1]) eq 6 then
        ff:=ClearDenominators(RR!(u^6*Evaluate(p[1],[s/u,t/u])));
      end if;
    end for;
// Check Picard number:
    R:=Deg2K3RankUpperBound(ff,20);
    PicNr := R[1]; // if eq 2 the while loop stops..
  end if;
until PicNr eq 2;

// Below is the output of the file:

"A smooth quartic X is found with an involution and Picard rank 2: ";

"The K3 surface X is: ";
X;
"The curve C of genus 2 on X, inducing the involution, is given by: ";
C;
"The surface X is a double cover of P2, with the covering map given by: ";
pi;
"This map is ramified over the curve defined by: ";
ff;


/* Output example:

A smooth quartic X is found with an involution and Picard rank 2: 

The K3 surface X is: 
Scheme over Rational Field defined by
6*x^2*y^2 + 3*y^4 + 6*x*z^3 + z^4 - 6*x^3*w + 7*x^2*y*w + 7*x*y^2*w + 10*y^3*w - 7*x^2*z*w - 7*x*z^2*w + 3*z^3*w - 3*x^2*w^2 - 11*x*y*w^2 + 10*y^2*w^2 - 7*x*z*w^2 + 8*z^2*w^2 - 10*x*w^3 - 10*z*w^3

The curve C of genus 2 on X, inducing the involution, is given by: 
Scheme over Rational Field defined by
-y*z + x*w,
-6*x*z^2 - z^3 + 6*x^2*w + 3*y^2*w + 7*x*z*w - 3*z^2*w + 7*x*w^2 + 10*y*w^2 - 8*z*w^2 + 10*w^3,
6*x^2*y + 3*y^3 - 6*x^2*z - x*z^2 + 7*x^2*w + 7*x*y*w + 10*y^2*w - 3*x*z*w - 8*x*w^2 + 10*y*w^2

The surface X is a double cover of P2, with the covering map given by:
Mapping from: Sch: X to Prj: P2
with equations : 
x*y - z^2
-y*z + x*w
y^2 - z*w

This map is ramified over the curve defined by: 
36*s^6 - 12*s^5*t - 83*s^4*t^2 - 50*s^3*t^3 + 55*s^2*t^4 - 306*s*t^5 - 231*t^6 - 60*s^5*u + 14*s^4*t*u + 86*s^3*t^2*u - 176*s^2*t^3*u + 406*s*t^4*u + 478*t^5*u + 37*s^4*u^2 - 22*s^3*t*u^2 + 562*s^2*t^2*u^2 + 356*s*t^3*u^2 - 213*t^4*u^2 + 
    182*s^3*u^3 + 62*s^2*t*u^3 - 278*s*t^2*u^3 - 102*t^3*u^3 - 15*s^2*u^4 + 42*s*t*u^4 + 149*t^2*u^4 + 236*s*u^5 + 60*t*u^5 - 20*u^6
*/
